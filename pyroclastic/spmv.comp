// Sparse Matrix vector multiplication
// The memory representation of M (or submatrices of M) will be:
// - a dense block of 30-50 columns with high density
// - a CSR for coefficients +1 (an index may appear multiple times for larger
// values)
// - a CSR for coefficients -1
//
// Indices are stored as 16-bit values so the maximal matrix dimension is 65536.

#version 450

#ifndef N
#error Matrix dimension undefined
#endif

#ifndef DENSE_N
#error Dense width undefined
#endif

#extension GL_EXT_control_flow_attributes : require
#extension GL_EXT_shader_explicit_arithmetic_types_int8 : require
#extension GL_EXT_shader_explicit_arithmetic_types_int16 : require
#extension GL_EXT_shader_explicit_arithmetic_types_int64 : require
#extension GL_EXT_shader_atomic_int64 : require

// Each thread handles a row.
layout(local_size_x = 128, local_size_y = 1, local_size_z = 1) in;

layout(binding = 0) readonly buffer Dense { int8_t dense[]; };
layout(binding = 1) readonly buffer SparsePlus { uint16_t plus[]; };
layout(binding = 2) readonly buffer SparseMinus { uint16_t minus[]; };
layout(binding = 3) readonly buffer IdxPlus { uint idxPlus[]; };
layout(binding = 4) readonly buffer IdxMinus { uint idxMinus[]; };
// Vector of size 2N:
// even iterations do V[N:2N] = M * V[0:N]
// odd iterations do V[0:N] = M * V[N:2N]
layout(binding = 5) buffer V { uint v[]; };
// Vector of identical values (1 per workgroup) indicating iteration count
layout(binding = 6) buffer Iter { uint iter[]; };
// Output mask for Wiedemann algorithm sequence.
layout(binding = 7) coherent buffer Wsel { uint8_t wsel[]; };
// Output buffer for Wiedemann algorithm sequence: wout[iter] = sum(v[idx] where wsel[idx]=1)
layout(binding = 8) coherent buffer Wout { uint64_t wout[]; };
layout(binding = 9) readonly buffer Moduli { uint moduli[]; };

int fastmod(int x, uint p) {
  float pinv = 1.0 / float(p);
  return x - int(p) * int(floor(float(x) * pinv));
}

void main() {
  uint idx = iter[gl_WorkGroupID.x];
  uint off0 = ((idx & 1) == 0) ? 0 : N;
  uint off1 = ((idx & 1) == 0) ? N : 0;
  uint p = moduli[0];
  uint row = gl_GlobalInvocationID.x;
  if (row >= N) return;
  int acc = 0;
  // Dense block
  uint dense_base = row * DENSE_N;
  for (uint i = 0; i < DENSE_N; i++) {
    acc += int(dense[dense_base + i]) * int(v[off0 + i]);
  }
  // +1 coefficients
  uint plus0 = idxPlus[row];
  uint plus1 = idxPlus[row + 1];
  for (uint i = plus0; i < plus1; i++) {
    acc += int(v[off0 + plus[i]]);
  }
  // -1 coefficients
  uint minus0 = idxMinus[row];
  uint minus1 = idxMinus[row + 1];
  for (uint i = minus0; i < minus1; i++) {
    acc -= int(v[off0 + minus[i]]);
  }
  // Output result to next row
  acc = fastmod(acc, p);
  v[off1 + row] = uint(acc);
  barrier();
  if (gl_LocalInvocationID.x == 0)
	  iter[gl_WorkGroupID.x] = idx + 1;
  if (gl_LocalInvocationID.x == wsel[gl_WorkGroupID.x])
	  atomicAdd(wout[idx], acc);
}
